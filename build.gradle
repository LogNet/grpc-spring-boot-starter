apply plugin: 'idea'
buildscript {
    ext {
        springBootVersion = '1.4.4.RELEASE'
        grpcVersion = '1.1.1'
    }
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/"}

      }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        classpath group: 'com.netflix.nebula', name: 'nebula-project-plugin', version: '2.2.1'
        classpath group: 'com.netflix.nebula', name: 'nebula-interactive', version: '2.0.+'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}



subprojects {

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'nebula-interactive'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencies{
        compileOnly('org.projectlombok:lombok:1.16.6')
    }

}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
